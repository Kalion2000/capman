# Maintainer: William Turner <willtur.will@gmail.com>

pkgname=pacman
pkgver=5.1.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme' 'pacman-mirrorlist' 'capman-keyring')
makedepends=('asciidoc')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
backup=(usr/local/etc/pacman.conf
        usr/local/etc/makepkg.conf)
options=('strip')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=("https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz"{,.sig}
        pacman.conf
        makepkg.conf)
sha256sums=('ce4eef1585fe64fd1c65c269e263577261edd7535fe2278240103012d74b6ef6'
            'SKIP'
            '5dd50cf63620970f6acd509c2608fb7ad7e269b187ca8b822739f23836a03d0c'
            '2d1e37e1488f0852b3deff9ead84e1ba1e9155909253282300e0eea9960523f3')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr/local \
    --libdir=/usr/local/lib64 \
    --with-makepkg-template-dir=/usr/local/share/makepkg-template \
    --with-scriptlet-shell=/bin/bash \
    --with-ldconfig=/sbin/ldconfig \
    --enable-doc

  make V=1
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -d -m755 "$pkgdir/usr/local/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/usr/local/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/usr/local/etc"

  # put bash_completion in the right location
  install -d -m755 "$pkgdir/usr/local/share/bash-completion/completions"
  mv "$pkgdir/usr/local/etc/bash_completion.d/pacman" "$pkgdir/usr/local/share/bash-completion/completions"
  rmdir "$pkgdir/usr/local/etc/bash_completion.d"

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/local/share/bash-completion/completions/$f"
  done
}
