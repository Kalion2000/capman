# Maintainer: William Turner <willtur.will@gmail.com>

pkgbase=vim
pkgname=('vim' 'vim-runtime')
pkgver=8.1.0648
_versiondir=81
pkgrel=1
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
arch=('x86_64')
url='https://www.vim.org'
license=('custom:vim')
makedepends=('glibc' 'libgcrypt' 'python2' 'python' 'ruby' 'lua' 'gawk' 'tcl' 'pcre' 'zlibpkg' 'libffi')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/vim/vim/archive/v${pkgver}.tar.gz"
        'vimrc'
        'capman.vim'
        'vimdoc.hook')
sha256sums=('7e6ad44dbb8fda0aca91c22fa0dcaed2d845cf00c26d6d3df3bfaa38c9da222a'
            '8241872ab571b1ee62fa1c7cf7922037d0cfe12ee45c3eda90196418a66df9b9'
            '5cb65f377537030cfae62a1622d39bf3dbc84fe288ecc186a492eef1c46a8da6'
            '64ef25df22e3de40322eb7c422a3aea22bfb72eca552f3386a94fbc791cbcb8a')

prepare() {
  cd "vim-${pkgver}/src"
  # define the place for the global (g)vimrc file (set to /usr/local/etc/vimrc)
  sed -i 's|^.*#define SYS_VIMRC_FILE.*$|#define SYS_VIMRC_FILE \"/usr/local/etc/vimrc\"|' feature.h
  sed -i 's|^.*#define SYS_GVIMRC_FILE.*$|#define SYS_GVIMRC_FILE \"/usr/local/etc/gvimrc\"|' feature.h
  autoconf
}

build() {
  cd "vim-${pkgver}"
  ./configure \
    --prefix=/usr/local \
    --with-features=huge \
    --with-compiledby='Capman' \
    --with-x=no \
    --disable-gui \
    --enable-acl \
    --enable-multibyte \
    --enable-cscope \
    --enable-netbeans \
    --enable-perlinterp=dynamic \
    --enable-pythoninterp=dynamic \
    --enable-pythoninterp=dynamic \
    --enable-rubyinterp=dynamic \
    --enable-luainterp=dynamic \
    --enable-tclinterp=dynamic \
    --disable-selinux
  make
}

package_vim-runtime() {
  pkgdesc+=' (shared runtime)'
  optdepends=('sh: support for some tools and macros'
              'python: demoserver example tool'
              'gawk: mve tools upport')
  backup=('usr/local/etc/vimrc')

  cd "vim-${pkgver}"

  make VIMRCLOC=/usr/local/etc DESTDIR="${pkgdir}" install
  # man and bin files belong to 'vim'
  rm -r "${pkgdir}/usr/local/share/man" "${pkgdir}/usr/local/bin"

  # patch filetype.vim for better handling of pacman related files
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    "${pkgdir}/usr/local/share/vim/vim${_versiondir}/filetype.vim"
  sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
    "${pkgdir}/usr/local/share/vim/vim${_versiondir}/ftplugin/changelog.vim"

  # rc files
  install -D -m 644 "${srcdir}/vimrc" "${pkgdir}/usr/local/etc/vimrc"
  install -D -m 644 "${srcdir}/capman.vim" "${pkgdir}/usr/local/share/vim/vimfiles/capman.vim"

  # no desktop files and icons
  rm -r "${pkgdir}/usr/local/share/"{applications,icons}

  # license
  install -d -m 755 "${pkgdir}/usr/local/share/licenses/vim-runtime"
  ln -s "/usr/local/share/vim/vim${_versiondir}/doc/uganda.txt" "${pkgdir}/usr/local/share/licenses/vim-runtime/license.txt"

  # pacman hook for documentation helptags
  install -D -m 644 "${srcdir}/vimdoc.hook" "${pkgdir}/usr/local/share/libalpm/hooks/vimdoc.hook"
}

package_vim() {
  depends=("vim-runtime=${pkgver}-${pkgrel}" 'acl' 'glibc' 'libgcrypt' 'pcre' 'zlibpkg' 'libffi')
  optdepends=('python2: Python 2 language support'
              'python: Python 3 language support'
              'ruby: Ruby language support'
              'lua: Lua language support'
              'perl: Perl language support'
              'tcl: Tcl language support')
  conflicts=('gvim')

  cd "vim-${pkgver}"
  make VIMRCLOC=/usr/local/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}/usr/local/bin/"{ex,view}

  # delete some manpages
  find "${pkgdir}/usr/local/share/man" -type d -name 'man1' 2> /dev/null | while read _mandir; do
    rm -f "${_mandir}/"{ex,view}.1 # provided by (n)vi
    rm -f "${_mandir}/evim.1"      # this does not make sense if we have no GUI
  done

  # Runtime provided by runtime package
  rm -r "${pkgdir}/usr/local/share/vim"

  # no desktop files and icons
  rm -r "${pkgdir}/usr/local/share/"{applications,icons}

  # license
  install -D -m 644 runtime/doc/uganda.txt "${pkgdir}/usr/local/share/licenses/${pkgname}/license.txt"
}

# vim: ts=2 sw=2 et:
