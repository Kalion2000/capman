# Maintainer: William Turner <willtur.will@gmail.com>

pkgname=pacman
pkgver=5.1.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash' 'glibc' 'libarchive' 'curl' 'gpgme')
makedepends=('asciidoc')
optdepends=('perl-locale-gettext: translation support in makepkg-template')
backup=("usr/local/etc/pacman.conf"
        "usr/local/etc/makepkg.conf")
options=('strip')
source=("https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz"
        pacman.conf
	makepkg.conf)
sha256sums=('ce4eef1585fe64fd1c65c269e263577261edd7535fe2278240103012d74b6ef6'
            '3525b63d24e2f4dba5cfed1a3d9afc3ab2aa93e991939533807ed134b98d2276'
            '4af50b2c5a54b06233766d747d76e3e1786fc2402af717f5fbdbbdc5f53e8d9f')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr/local \
    --libdir=/usr/local/lib64 \
    --with-scriptlet-shell=/bin/bash \
    --with-ldconfig=/sbin/ldconfig \
    --with-makepkg-template-dir=/usr/local/share/makepkg-template \
    --enable-doc

  make V=1
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/local/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/usr/local/etc"
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/usr/local/etc"
}
